apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
  publications {
    ltr(MavenPublication) {
      from components.java
      afterEvaluate {
        artifactId = 'elasticsearch-learning-to-rank'
      }

      pom {
        name = 'Elasticsearch Learning to Rank plugin'
        description = 'Learning to Rank Query w/ RankLib Models'

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'osc'
            name = 'Oscar'
            email = 'oscar@o19s.com'
          }
        }
      }
    }
  }

  repositories {
    maven {
      name = "MavenCentral"
      url = System.getenv('SONATYPE_REPO_URL')

        credentials {
          username = System.getenv('SONATYPE_USER')
          password = System.getenv('SONATYPE_PASS')
        }
    }
  }
}

signing {
  required { gradle.taskGraph.hasTask('publish') }
  def signingKey = System.getenv('GPG_SIGNING_KEY')
  def signingPass = System.getenv('GPG_SIGNING_PASS')

  useInMemoryPgpKeys(signingKey, signingPass)
  sign publishing.publications.ltr
}
